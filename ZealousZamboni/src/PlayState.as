package
{
	import adobe.utils.CustomActions;
	import org.flixel.*;
	import org.as3commons.collections.ArrayList;
	/**
	 * ...
	 * @author Kenny
	 * This is the actual in-game playing state that controls most of the game
	 */
	public class PlayState extends FlxState
	{
		[Embed(source = "../res/tiles.jpg")] public var TileSheet:Class;
		[Embed(source = '../res/level1.txt', mimeType = "application/octet-stream")] public var Level1Csv:Class;
		
		//Size of tiles in pixels
		private static const TILE_SIZE:int = 8;

		//Set of all blocks in the level
		private var level:FlxTilemap;
		
		//Set of all sprites active in the level (including the player)
		private var activeSprites:FlxGroup;
		private var player:Zamboni;
		private var movables:ArrayList;
		
		public function PlayState() {
			
		}
		
		override public function create() : void {
			FlxG.bgColor = 0xffaaaaaa;
			//TODO Replace the following with something that actually loads levels
			level = new FlxTilemap();
			//level.loadMap(FlxTilemap.arrayToCSV(data, 40), FlxTilemap.ImgAuto, 0, 0, FlxTilemap.AUTO);
			level.loadMap(new Level1Csv(), TileSheet, TILE_SIZE, TILE_SIZE, FlxTilemap.OFF, 0, 0, 1);
			activeSprites = new FlxGroup();
			movables = new ArrayList();
			add(level);
			player = new Zamboni(FlxG.width / 2 - 5);
			addUnit(player);
			addUnit(new Skater(50, 25));
			addUnit(new Skater(128,192));
			FlxG.mouse.show();
		}
		
		//Adds a ZzUnit to the appropriate lists
		//This should be called only when the target unit should be placed in the game
		public function addUnit(z:ZzUnit) : void {
			movables.add(z);
			activeSprites.add(z);
			add(z);
			if (z is Skater) {
				add(Skater(z).getTrail());
				activeSprites.add(Skater(z).getTrail());
			}
		}
		
		override public function update():void
		{
			/*player.acceleration.x = 0;
			player.acceleration.y = 0;
			FlxG.mouse.getWorldPosition
			if(FlxG.keys.LEFT)
				player.acceleration.x = -player.maxVelocity.x*4;
			if(FlxG.keys.RIGHT)
				player.acceleration.x = player.maxVelocity.x*4;
			if (FlxG.keys.UP)
				player.acceleration.y = -player.maxVelocity.y * 4;
			if (FlxG.keys.DOWN)
				player.acceleration.y = player.maxVelocity.y * 4;*/
			var mouse:FlxPoint = FlxG.mouse.getWorldPosition(); //mouse coordinates
			
			var z:FlxPoint = player.getMidpoint();	//player coordinates
			var n:Number = 5;	//tolerance in pixels
			if(FlxG.mouse.pressed()){
				if (mouse.x < z.x - n) {
					player.velocity.x = -player.maxVelocity.x;
				}else if (mouse.x - n > z.x) {
					player.velocity.x = player.maxVelocity.x;
				}else {
					player.velocity.x = 0;
				}
				if (mouse.y < z.y - n) {
					player.velocity.y = -player.maxVelocity.y;
				}else if (mouse.y - n > z.y) {
					player.velocity.y = player.maxVelocity.y;
				}else {
					player.velocity.y = 0;
				}
			}else {
				player.velocity.x = 0;
				player.velocity.y = 0;
			}
			
			super.update();
			for each(var m:FlxBasic in members) {
				if (!m.exists) {
					remove(m);
				}
			}
			FlxG.collide(level, activeSprites, onCollision);
			FlxG.collide(activeSprites, activeSprites, onCollision);
		}
		
		private function onCollision(a:FlxObject, b:FlxObject) : void {
			if (a is ICollidable) {
				ICollidable(a).onCollision(b);
			}
			if (b is ICollidable) {
				ICollidable(b).onCollision(a);
			}
		}
		
	}
	
}